#@TYPE: Machine
#@NAME: icicle-kit-es-auth
#@SOC: Polarfire SoC
#@DESCRIPTION: Machine configuration for the Microchip Polarfire SoC, Icicle Kit Engineering Sample Authenticated boot

require conf/machine/include/riscv/tune-riscv.inc
require include/authenticated-boot.inc

MACHINE_FEATURES = "screen keyboard ext2 ext3 serial"
MACHINE_TYPE = "smp"

KERNEL_CLASSES = "kernel-fitimage"
KERNEL_IMAGETYPE = "fitImage"
EXTERNAL_KERNEL_DEVICETREE = "${DEPLOY_DIR_IMAGE}/overlays"

RISCV_SBI_FDT ?=  "mpfs-icicle-kit.dtb"
KERNEL_DTC_FLAGS += "-@"
KERNEL_DEVICETREE ?= "microchip/${RISCV_SBI_FDT}"
KBUILD_DEFCONFIG ?= "mpfs_defconfig"

PREFERRED_PROVIDER_virtual/kernel ?= "mpfs-linux"

PREFERRED_VERSION_openocd-native = "riscv"
PREFERRED_VERSION_openocd = "riscv"

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-mpfs"
PREFERRED_PROVIDER_u-boot = "u-boot-mpfs"

## Set this to "mpfs_icicle" to use a nand flash as the default external flash memory
## Set this to "mpfs_icicle_nor" to use a nor flash as the default external flash memory
## Note: If no external flash memory is found, U-boot will try to boot from SD/emmc 
## or fallback to tftp

UBOOT_CONFIG = "mpfs_icicle"
UBOOT_CONFIG[mpfs_icicle] = "microchip_mpfs_icicle_defconfig"
UBOOT_CONFIG[mpfs_icicle_nor] = "microchip_mpfs_icicle_qspi_nor_defconfig"

INCLUDES += "${@bb.utils.contains('UBOOT_CONFIG', 'mpfs_icicle', \
' include/qspi-flash-nand.inc', ' include/qspi-flash-nor.inc', d)}"
require ${INCLUDES}

UBOOT_ENTRYPOINT = "0x80200000"
UBOOT_DTB_LOADADDRESS = "0x8a000000"
UBOOT_DTBO_LOADADDRESS = "0x8a080000"
UBOOT_DTBO_OFFSET = "0x00010000"
UBOOT_RD_LOADADDRESS = "0x8aa00000"
UBOOT_RD_ENTRYPOINT = "0x8aa00000"

HSS_PAYLOAD = "uboot"

MACHINE_EXTRA_RRECOMMENDS += " kernel-modules"

IMAGE_FSTYPES += "wic wic.gz wic.bmap ext4"
IMAGE_CLASSES:append = " image_type_mtd"

## Do not update fstab file when using wic images
WIC_CREATE_EXTRA_ARGS ?= "--no-fstab-update"

## wic default support
WKS_FILE_DEPENDS ?= " \
    e2fsprogs-native \
    bmap-tools-native \
    u-boot-mpfs \
"

IMAGE_BOOT_FILES ?= " \
    fitImage \
"

WKS_FILE ?= "mpfs-rootfs.wks"

